#!/usr/bin/perl

use strict;
use warnings;
use utf8;

use Cwd 'abs_path';
use File::Basename;
use List::Util qw[min max];

use lib dirname (__FILE__);
use ForeachNewRev;

my $checker_dir = dirname(abs_path(__FILE__)) . '/FCHK-checks/';

my @checkers = @ARGV[7..$#ARGV];
my $checker_length = 0;
foreach my $checker (@checkers) {
  $checker_length = max($checker_length, length $checker);
}

foreach_new_rev(sub {
  my ($commit) = @_;

  my $result = '';

  open GH, '-|', 'git', 'show', '--pretty=format:', '--name-status', $commit
    or die "failed: git show $commit: $!";
  my @files = <GH>;
  close GH;
  chomp @files;

  foreach my $status_file (@files) {
    my ($status, $file) = split("\t", $status_file);
    next if (!length $file);

    foreach my $checker (@checkers) {
      my ($exe, @arg) = split('@', $checker);
      if (system($checker_dir . $exe, $commit, $status, $file, @arg) != 0) {
        $result .= sprintf "    %- " . $checker_length . "s  @  %s\n", $checker, $file;
      }
    }
  }

  return $result;
});
